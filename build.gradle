apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'io.validly'
version = '0.1.0'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.5'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {

            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment)
            }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'Validly'
                packaging 'jar'
                description 'Library that provides a way to create declarative validation logic with Java 8'
                url 'http://validly.io'

                scm {
                    connection 'scm:git:git://github.com/larcki/validly.git'
                    url 'https://github.com/larcki/validly/tree/master'
                    developerConnection 'scm:git:ssh://github.com:larcki/validly.git'
                }

                licenses {
                    license {
                        name 'Apache License 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        name 'Lassi Kapraali'
                    }
                }
            }
        }
    }
}
